buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.minecraftforge.net/' }
    }
    dependencies {
        classpath 'com.github.hard2make1name:ForgeGradle:e682169'
        classpath 'com.github.Skytils:mixingradle:d5f9873d60'
        classpath 'com.github.johnrengelman:shadow:6.1.0'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url = 'https://repo.sk1er.club/repository/maven-public' }
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
}

dependencies {
    implementation('org.json:json:20220924')
    implementation 'org.mongodb:mongo-java-driver:3.12.11'
    implementation('gg.essential:loader-launchwrapper:1.1.2')
    implementation('gg.essential:essential-1.8.9-forge:1733') {
        exclude module: 'gson'
    }

    implementation('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }

    annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
}

shadowJar {
    dependencies {
        include(dependency('org.json:json:20220924'))
        include(dependency('gg.essential:loader-launchwrapper:1.1.2'))
        include(dependency('org.spongepowered:mixin:0.7.11-SNAPSHOT'))
        include(dependency('org.mongodb:mongo-java-driver:3.12.11'))
    }
    exclude('dummyThing')
    exclude('META-INF/maven/')
}

reobf {
    shadowJar {} // Reobfuscate the shadowed JAR
}

version = "1.0"
group = "wacky.steve"

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    mappings = "stable_22"
    clientRunArgs += "--tweakClass org.spongepowered.asm.launch.MixinTweaker"
    clientRunArgs += "--mixin mixins.wacky.json"
    // clientRunArgs += ["--tweakClass=gg.essential.loader.stage0.EssentialSetupTweaker"]
}

jar {
    manifest.attributes(
            'FMLCorePlugin': "${project.group}.tweaker.WackySteveLoadingPlugin",
            'ForceLoadAsMod': true,
            'FMLCorePluginContainsFMLMod': true,
            'MixinConfigs': 'mixins.wacky.json',
            'ModSide': 'CLIENT',
            'TweakClass': 'gg.essential.loader.stage0.EssentialSetupTweaker'
    )
}

sourceJar {
    enabled = false
}

mixin {
    add sourceSets.main, "mixins.wacky.refmap.json"
}

// Automatic version replacements
/*
task processSource(type: Sync) {
    from sourceSets.main.java
    inputs.property 'version', project.version
    filter(ReplaceTokens, tokens: [version: project.version])
    into "$buildDir/src"
}
*/

processResources {
    inputs.property 'version', project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}